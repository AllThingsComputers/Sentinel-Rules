// Title of Detection: suspect DNS Indicators from Client  
// Authorship & Contact: https://allthingscomputers.medium.com  https://twitter.com/allthingshandle  https://allthingscomputersblog.wordpress.com/ 
// Description of the Detection:  https://blog.menasec.net/2019/02/threat-hunting-24-microsoft-windows-dns.html 
// References (if available): 
// The Logic of the Detection or Query Begins Below This Line
(union isfuzzy=true
    (
        DnsEvents
        | where EventId in (256, 257) 
        and Name matches regex @"(?i)myip|ipchicken|ipinfo|ipaddr|meineip|meuip|portquiz|portchecktool|ipid|iptools|hostip|canyouseeme"
        | extend Description = "DNS requests to public IP online resolution web services"
    ), 
    (
        DnsEvents
        | where Name matches regex @"(?i)country$|stream$|download$|xin$|gdn$|racing$|jetzt$|win$|bid$|vip$|ren$|kim$|loan$|mom$|party$|review$|trade$|date$|wang$|accountants$"
        | extend Description = "DNS request to suspect TLDs"
    ), 
    (
        DnsEvents
        | where EventId in (256, 257) 
        and QueryType in (16, 46)
        | extend Description = "DNS TXT or RRSIG Exfiltration"
    ), 
    (
        DnsEvents
        | where EventId in (256, 257) 
        and QueryType == 1 
        and ResultCode == 3
        | extend Description = "Too many NX response from same source IP"
    ),
    (
        DnsEvents
        | where EventId == 257 
        and strlen(Name) > 80 
        and QueryType != 249 
        and not(Name matches regex @"(?i)exclusions|here")
        | extend Description = "DNS requests to Very Long Domain Names"
    )
)
