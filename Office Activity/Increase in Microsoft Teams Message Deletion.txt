// Title of Detection: Increase in Microsoft Teams Message Deletion
// Authorship & Contact: https://allthingscomputers.medium.com  https://twitter.com/allthingshandle  https://allthingscomputersblog.wordpress.com/ 
// Description of the Detection: This will trigger when thereâ€™s an uncharacteristic increase in Microsoft Teams message deletions against their personal baselines, potentially indicating suspicious removal of messages.
// References (if available): 
// The Logic of the Detection or Query Begins Below This Line

// Setting the variables for lookback time and thresholds for baseline and new deletions
let baselineLookback = 21d;
let baselineToRemoveHistoricHighDeleters = 30;
let recentActivityLookbackTime = 3d;
let recentActivityMinimumDeletes = 8;

// Baseline list of users who are high message deleters over the lookback period (whitelisted)
let HighMessageDeleters = 
    OfficeActivity
    | where TimeGenerated > ago(baselineLookback) and TimeGenerated < ago(recentActivityLookbackTime)
    | where OfficeWorkload =~ "MicrosoftTeams"
    | where Operation =~ "MessageDeleted"
    | summarize count() by UserId
    | where count_ > baselineToRemoveHistoricHighDeleters;

// Detecting an increase in message deletions outside the baseline for those not in the high-deleter list
OfficeActivity
| where TimeGenerated > ago(recentActivityLookbackTime)
| where OfficeWorkload =~ "MicrosoftTeams"
| where Operation =~ "MessageDeleted"
| where UserId !in (HighMessageDeleters)  // Exclude high deleters
| summarize count() by UserId
| where count_ > recentActivityMinimumDeletes  // Threshold for recent deletions
| order by count_
