// Title of Detection: Successful Login with Password Older than Expiry Date
// Authorship & Contact: https://allthingscomputers.medium.com  https://twitter.com/allthingshandle  https://allthingscomputersblog.wordpress.com/
// Description of the Detection:
// References: 

let PasswordLastSet = SecurityEvent
| where TimeGenerated >= ago(180d)
| where EventID in (4723, 4724) // password change events
| summarize arg_max(TimeGenerated, *) by TargetAccount
| project TargetAccount, TimeOfPWChange = TimeGenerated;

let LogonSuccessToday = SecurityEvent
| where TimeGenerated >= ago(24h)
| where EventID == 4624 // successful login events
| summarize arg_max(TimeGenerated, *) by TargetAccount
| project TargetAccount, TimeOfLogin = TimeGenerated;

PasswordLastSet
| join kind=inner (LogonSuccessToday) on TargetAccount
| extend NoDaysSinceReset = (TimeOfLogin - TimeOfPWChange) / 1d
| where NoDaysSinceReset > 46 // passwords older than 46 days
| project TargetAccount, TimeOfPWChange, TimeOfLogin, NoDaysSinceReset
