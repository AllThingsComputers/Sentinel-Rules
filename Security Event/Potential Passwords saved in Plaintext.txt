// Title of Detection: Potential Passwords Saved in Plaintext
// Authorship & Contact: https://allthingscomputers.medium.com, https://twitter.com/allthingshandle, https://allthingscomputersblog.wordpress.com/
// Description: Detects files with potential password-related keywords that have not been recycled or renamed to reduce false positives.
// References: https://www.passcamp.com/blog/dangers-of-storing-and-sharing-passwords-in-plaintext/

let PasswordSynonyms = dynamic(["password", "p@ssword", "PW", "pwd", "passphrase", "private key", "key"]);
let FPfiles = dynamic(["sample.mdb", "README.md"]);
let packageFPExtensions = dynamic(["js", "ts", "json", "html", "yml", "css", "map"]);
let commonExtensions = dynamic(["doc", "pdf", "xlsx", "txt", "csv", "png", "jpg", "zip", "msg", "one", "aspx", "rtf", "pptx", "xml", "exe", "jpeg", "7z", "gif", "docx"]);

// Files that were recycled or renamed
let RecycledRenamed = OfficeActivity
| where OfficeObjectId has_any (PasswordSynonyms)
| where Operation has_any("FileDeletedFirstStageRecycleBin", "FolderDeletedFirstStageRecycleBin", "FileDeletedSecondStageRecycleBin", "FolderDeletedSecondStageRecycleBin", "FileDeleted", "FileVersionsAllDeleted", "FileRecycled", "FolderRecycled", "FileRenamed", "FolderRenamed")
| summarize count() by OfficeObjectId, SourceFileName, SourceFileExtension, SourceRelativeUrl;

// Newly created or renamed files
let CreatedRenamed = OfficeActivity
| where OfficeObjectId has_any (PasswordSynonyms)
| where SourceFileName !in (FPfiles)
| where SourceFileExtension !in (packageFPExtensions) and SourceFileExtension != ""
| where SourceFileExtension in (commonExtensions)
| where Operation has_any("FileSyncUploadedFull", "FolderCreated", "FileUploaded")
| summarize count() by OfficeObjectId, SourceFileName, SourceFileExtension, SourceRelativeUrl, UserId;

// Exclude files that have been recycled or renamed
CreatedRenamed
| join kind=leftanti (RecycledRenamed) on OfficeObjectId
| sort by OfficeObjectId asc
